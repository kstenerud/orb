dogma_v1 utf-8
- identifier  = ort_v1
- description = Object Representation in Text, version 1
- dogma       = https://github.com/kstenerud/dogma/blob/master/v1/dogma_v1.0.md

document               = (WSC* & record_type)* value;

value                  = WSC* & (string | number | boolean | null | object | array | typed_array | timestamp | uuid) & WSC*;
key                    = WSC* & (string | integer | timestamp | uuid) & WSC*;

record_type            ='@' & identifier & container('(', key, ')');
record                 ='@' & identifier & container('{', value, '}');
identifier             = char_identifier_first & char_identifier_next*;

array                  = container('[', value, ']');
object                 = container('{', key_value, '}');
boolean                = true | false;
uuid                   = digit_0_f{8} & '-'
                       & digit_0_f{4} & '-'
                       & digit_0_f{4} & '-'
                       & digit_0_f{4} & '-'
                       & digit_0_f{12}
                       ;
string                 = '"' & (char | escape_sequence)* & '"';
escape_sequence        = '\\' & ( '"' | '\\' | '/' | 'b'
                                | 'f' | 'n' | 'r' | 't'
                                | ('u' & digit_0_f{4})
                                | ('[' & digit_0_f{1~8} & ']')
                                )
                       ;

number                 = '-'? & (ufloat_dec | ufloat_hex | inf | qnan | snan);
ufloat_dec             = uinteger_dec & fractional_dec? & exponent_dec?;
ufloat_hex             = uinteger_hex & fractional_hex? & exponent_hex?;
integer                = '-'? & uinteger;
uinteger               = uinteger_dec | uinteger_hex;
uinteger_dec           = '0' | (digit_1_9 & digit_0_9*);
uinteger_hex           = '0' | (digit_1_f & digit_0_f*);
fractional_dec         = '.' & digit_0_9+;
fractional_hex         = '.' & digit_0_f+;
exponent_dec           = ('e' | 'E') & ('+' | '-')? & uinteger_dec;
exponent_hex           = ('p' | 'P') & ('+' | '-')? & uinteger_dec;

timestamp              = year & '-' & month & '-' & day & 'T' & hour & ':' & minute & ':' & second & subsecond? & 'Z';
year                   = string_num_4(1900~2484);
month                  = string_num_2(1~12);
day                    = string_num_2(1~31);
hour                   = string_num_2(0~23);
minute                 = string_num_2(0~59);
second                 = string_num_2(0~60);
subsecond              = fractional_dec;

typed_array            = integer_array | uinteger_array | float_array | timestamp_array | uuid_array;
integer_array          = container('@i' & ('8' | '16' | '32' | '64') & '[', integer, ']');
uinteger_array         = container('@u' & ('8' | '16' | '32' | '64') & '[', uinteger, ']');
float_array            = container('@f' & ('16' | '32' | '64') & '[', number, ']');
timestamp_array        = container('@ts' & '[', timestamp, ']');
uuid_array             = container('@uuid' & '[', uuid, ']');

comment                = comment_single_line | comment_multi_line;
comment_single_line    = "//" & (char* ! LINE_END) & LINE_END;
comment_multi_line     = "/*" & ((char* ! "/*") | comment_multi_line) & "*/";

container(begin, type, end) = begin & WSC* & items(type)? & WSC* & end;
items(type)            = type & (WSC+ & type)*;
key_value              = key & ':' & value;

string_num_4(value)    = ('0' + (value/1000) % 10) & ('0' + (value/100) % 10) & ('0' + (value/10) % 10) & ('0' + value % 10);
string_num_2(value)    = ('0' + (value/10) % 10) & ('0' + value % 10);

true                   = 'true';
false                  = 'false';
null                   = 'null';
qnan                   = 'qnan';
snan                   = 'snan';
inf                    = 'inf';
digit_0_f              = '0' | digit_1_f;
digit_1_f              = digit_1_9 | 'a'~'f' | 'A'~'F';
digit_0_9              = '0' | digit_1_9;
digit_1_9              = '1'~'9';
WSC                    = WS | comment;
WS                     = HT | SP | LINE_END | ',';
LINE_END               = CR? & LF;
HT                     = '\[9]';
LF                     = '\[a]';
CR                     = '\[d]';
SP                     = '\[20]';
char_identifier_first  = unicode(L,N) | '_';
char_identifier_next   = unicode(Cf,L,M,N) | '_' | '.' | '-';
char                   = unicode(C,L,M,N,P,S,Z);
